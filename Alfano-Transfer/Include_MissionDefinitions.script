% Mission Definitions copied from 64x5999_16T_28.5i_20Jun2020_AlfanoXfer_r1_Run3.script

%----------------------------------------
%---------- Arrays, Variables, Strings
%----------------------------------------

Create Array CONTROL[1,3];
Create Variable SMA_INIT INC_INIT ORBIT_R AOL_LAST DELTA_AOL REV AOL INITIAL_FUEL FUEL;
GMAT CONTROL(1, 1) = 1;
GMAT SMA_INIT = 6878.1366;
GMAT ORBIT_R = 1;
GMAT AOL_LAST = 0;
GMAT DELTA_AOL = 0;
GMAT REV = 0;
GMAT AOL = 0;
GMAT INITIAL_FUEL = 0;
GMAT FUEL = 0;
GMAT INC_INIT = 0;


%----------------------------------------
%---------- Mission Sequence
%----------------------------------------

%----------------------------------------
%---------- Mission Sequence
%----------------------------------------

BeginMissionSequence;
Propagate 'Propagate to periapsis' TrimPropagator(EOTV) {EOTV.Earth.Periapsis};
GMAT SMA_INIT = EOTV.SMA;
GMAT AOL_LAST = EOTV.TA + EOTV.AOP;
GMAT INITIAL_FUEL = EOTV.RAPTank1.FuelMass;
GMAT INC_INIT = EOTV.INC;

% Propagate out to around 2000km to reduce effects of eclipse or to higher altitude orbit.

BeginFiniteBurn 'BeginAlfanoBurn' ContinuousThrust(EOTV);
While EOTV.SMA <= 42159
   % Argument of Latitude is used to fix the thrust application relative to the ECEF Frame
   GMAT AOL = EOTV.TA + EOTV.AOP;
   If AOL > 360.00
	  AOL = AOL - 360.00
   EndIf
   
   GMAT DELTA_AOL = AOL - AOL_LAST;
   
   If DELTA_AOL < 0
      GMAT REV = REV + 1;
   EndIf;
   GMAT AOL_LAST = AOL;
   
   GMAT [CONTROL] = Python.YawAngles.get_control_onrev(AOL, EOTV.Earth.SMA, SMA_INIT, INC_INIT);
   GMAT EOTV.HET1.ThrustDirection1 = CONTROL(1,1);
   GMAT EOTV.HET1.ThrustDirection2 = CONTROL(1,2);
   GMAT EOTV.HET1.ThrustDirection3 = CONTROL(1,3);
   
   Propagate 'Propagate Steps' DefaultProp(EOTV);
   
   If EOTV.INC < 0.05
      GMAT EOTV.HET1.ThrustDirection1 = 1;
      GMAT EOTV.HET1.ThrustDirection2 = 0;
      GMAT EOTV.HET1.ThrustDirection3 = 0;
      While EOTV.SMA <= 42159
         Propagate 'Propagate to Altitude' DefaultProp(EOTV);
      EndWhile;
   EndIf;
EndWhile;
EndFiniteBurn 'EndAlfanoBurn' ContinuousThrust(EOTV);

% We should also trim the eccentricity if > 0.02

